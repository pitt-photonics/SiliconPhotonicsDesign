# Electrical simulation of the pn-junction phase shifter
# Step 1: in Lumerical DEVICE; this script accomplishes:
# 1) Simulate the DC characteristics of the junction, 
#    to export the spatial charge density, for different voltages
# 2) Calculates the junction capacitance versus voltage.
# 3) Calculates the resistance in each slab

newproject; redrawoff;

# modulator geometry variables defined:
modulator_setup_parameters;

addmodelmaterial;
set('name', 'Si (Silicon)');
addmaterialproperties('CT','Si (Silicon)');

addmodelmaterial;
set('name','Al (Aluminium) - CRC');
addmaterialproperties('CT','Al (Aluminium) - CRC');

addmodelmaterial;
set('name','SiO2 (Glass) - Sze');
addmaterialproperties('CT','SiO2 (Glass) - Sze');

# draw geometry
addrect; 			# rib
set('name','rib');
set('material','Si (Silicon)');
set('x',x_center); set('x span', width_rib);
set('y',y_center); set('y span',y_span);
set('z min',thick_slab); set('z max',thick_slab+thick_rib);

addrect; 			# slab
set('name','slab');
set('material','Si (Silicon)');
set('x',x_center); set('x span', width_slab);
set('y',y_center); set('y span',y_span);
set('z min',z_center); set('z max',thick_slab);

addrect; 			# plateau
set('name','plateau_left');
set('material','Si (Silicon)');
set('x',-center_plateau); set('x span', width_plateau);
set('y',y_center); set('y span',y_span);
set('z min',z_center); set('z max',thick_slab+thick_rib);
copy;
set('name','plateau_right');
set('x',center_plateau);

addrect;			# contacts
set('name','anode');
set('material','Al (Aluminium) - CRC');
set('x',-center_contact); set('x span', width_contact);
set('y',y_center); set('y span',y_span);
set('z min',thick_slab+thick_rib); set('z max',thick_slab+thick_rib+thick_contact);
copy;
set('name','cathode');
set('x',center_contact);

addrect; 			# oxide
set('name','oxide');
set('material','SiO2 (Glass) - Sze');
set('mesh order',5);
set('override color opacity from material database',1); set('alpha',0.3);
set('x',x_center); set('x span',x_span);
set('y',y_center); set('y span',y_span);
set('z',z_center); set('z span',z_span);

# draw simulation region
addchargesolver;
set('min edge length',min_edge_length);
set('max edge length',max_edge_length);

select('simulation region');
set('x',x_center); set('x span',x_span-0.1e-6);
set('y',y_center); set('y span',y_span);
set('z',z_center); set('z span',z_span);

addchargemesh;
set('name','wg mesh');
set('max edge length',max_edge_length_override);
set('x',x_center); set('x span', width_rib);
set('y',y_center); set('y span',y_span);
set('z min',0); set('z max',thick_slab+thick_rib);
set('enabled',0);

# draw doping regions
adddope;
set('name','pepi');
set('dopant type','p');	# p type
set('x',x_center); set('x span',x_span);
set('y',y_center); set('y span',y_span);
set('z',center_pepi); set('z span',thick_pepi);

adddiffusion;	
set('name','p implant');
set('x',x_center_p); set('x span',x_span_p);
set('y',y_center); set('y span',y_span);
set('z',z_center_p); set('z span',z_span_p);
set('dopant type','p');
set('face type',face_p);
set('junction width',width_junction_p);
set('distribution index',diff_dist_fcn);
set('concentration',surface_conc_p);
set('ref concentration',reference_conc_p);

adddiffusion;
set('name','n implant');
set('x',x_center_n); set('x span',x_span_n);
set('y',y_center); set('y span',y_span);
set('z',z_center_n); set('z span',z_span_n);
set('dopant type','n');
set('face type',face_n);
set('junction width',width_junction_n);
set('distribution index',diff_dist_fcn);
set('concentration',surface_conc_n);
set('ref concentration',reference_conc_n);

adddiffusion;	
set('name','p++');
set('x',x_center_p_contact); set('x span',x_span_p_contact);
set('y',y_center); set('y span',y_span);
set('z',z_center_p_contact); set('z span',z_span_p_contact);
set('dopant type','p');
set('face type',face_p_contact);
set('junction width',width_junction_p_contact);
set('distribution index',diff_dist_fcn_contact);
set('concentration',surface_conc_p_contact);
set('ref concentration',reference_conc_p_contact);

adddiffusion;
set('name','n++');
set('x',x_center_n_contact); set('x span',x_span_n_contact);
set('y',y_center); set('y span',y_span);
set('z',z_center_n_contact); set('z span',z_span_n_contact);
set('dopant type','n');
set('face type',face_n_contact);
set('junction width',width_junction_n_contact);
set('distribution index',diff_dist_fcn_contact);
set('concentration',surface_conc_n_contact);
set('ref concentration',reference_conc_n_contact);

adddiffusion;	
set('name','p wg implant');
set('x',x_center_p_rib); set('x span',x_span_p_rib);
set('y',y_center); set('y span',y_span);
set('z',z_center_p_rib); set('z span',z_span_p_rib);
set('dopant type','p');
set('face type',face_p_rib);
set('junction width',width_junction_p_rib);
set('distribution index',diff_dist_fcn_rib);
set('concentration',surface_conc_p_rib);
set('ref concentration',reference_conc_p_rib);

adddiffusion;
set('name','n wg implant');
set('x',x_center_n_rib); set('x span',x_span_n_rib);
set('y',y_center); set('y span',y_span);
set('z',z_center_n_rib); set('z span',z_span_n_rib);
set('dopant type','n');
set('face type',face_n_rib);
set('junction width',width_junction_n_rib);
set('distribution index',diff_dist_fcn_rib);
set('concentration',surface_conc_n_rib);
set('ref concentration',reference_conc_n_rib);

# draw monitors
addchargemonitor;	# capacitance
set('monitor type',6); # 2D y-normal
set('integrate total charge',1);
set('x',x_center); set('x span',x_span);
set('z min',0); set('z max',thick_slab+thick_rib);
set('save data',1);
set('filename',filename_mzi);

# set contacts (boundary conditions)
addelectricalcontact;
set('name','anode');		# anode
set('bc mode','steady state');
set('sweep type','single');
set('voltage',0);
set('surface type', 'solid');
set('solid','anode');

addelectricalcontact;
set('name','cathode');
set('surface type', 'solid');
set('solid','cathode');

set('bc mode','steady state');
set('sweep type','range');
set('range start',voltage_start);
set('range stop',voltage_stop);
set('range interval',voltage_interval);

# 1) Simulate the DC characteristics of the junction, 
#    export the spatial charge density, for different voltages
save('pn_wg_dcsweep.ldev');
run;

# 2) Calculates the junction capacitance versus voltage.
#    compare with experimental results
CV_baehrjones = [-0.4, 0.261; -0.25, 0.248; 0, 0.223; 0.25, 0.208; 0.5, 0.198; 0.75, 0.190; 1.0, 0.184; 1.5, 0.175; 2.0, 0.168; 3.0, 0.157; 4.0, 0.150]; 

# perform two simulations, separated by 'dv', for each voltage step.
# this is used to determine the change in charge for the small voltage change,
# to find the capacitance.
vmin = -0.4; vmax = 4; N = 12;
dv = 0.025;
vdv = matrix(2*N,1);
vdv(1:2:(2*N)) = linspace(vmin,vmax,N);
vdv(2:2:(2*N)) = vdv(1:2:(2*N)) + sign(vmax)*dv;

pause;

switchtolayout;

# set contact bias
select('CHARGE::boundary conditions::cathode');
set('sweep type','value');
set('value table',vdv);

# set correct solver parameters (need Fermi stats for high doping)
setnamed('CHARGE','solver type','newton');
#setnamed('CHARGE','fermi statistics',true);

# refine mesh for C calculation
setnamed('CHARGE::wg mesh','enabled',1);

# don't save this result to file
setnamed('CHARGE::monitor','save data',0);

save('pn_wg_cvanalysis.ldev');
run;

total_charge = getresult('CHARGE::monitor','total_charge');
Qn = e*pinch(total_charge.n);
Qp = e*pinch(total_charge.p);

Cn = abs(Qn(2:2:(2*N))-Qn(1:2:(2*N)))/dv;
Cp = abs(Qp(2:2:(2*N))-Qp(1:2:(2*N)))/dv;
V = vdv(1:2:(2*N));

# User should check for convergence; Cn and Cp should be equal:
#plotxy(V,Cn*1e15*1e-6,V,Cp*1e15*1e-6,"Voltage (V)","Capacitance (fF/um)");

# Final result:
plotxy(V,0.5*(Cn+Cp)*1e15*1e-6,CV_baehrjones(1:11,1),CV_baehrjones(1:11,2),"Voltage (V)","Capacitance (fF/um)");
legend('TCAD Model','Experimental');

#
# 3) Calculate the resistance in each slab
#    Add a contact in the middle then adjust simulation region
switchtolayout;

addrect; 			# rib contact
set('name','r_contact');
set('material','Al (Aluminium) - CRC');
set('x',x_center); set('x span', 0.5*width_rib);
set('y',y_center); set('y span',y_span);
set('z min',z_center); set('z max',thick_slab+thick_rib+thick_contact);
set('mesh order',1);

vtest_max = 0.5;
addelectricalcontact;
set('name','r_test');
set('surface type', 'solid');
set('solid','r_contact');

set('bc mode','steady state');
set('sweep type','range');
set('range start',0);
set('range stop',vtest_max);
set('range interval',0.1);

setnamed('CHARGE::boundary conditions::anode','bc mode','steady state');
setnamed('CHARGE::boundary conditions::anode','sweep type','single');
setnamed('CHARGE::boundary conditions::anode','voltage',0);
setnamed('CHARGE::boundary conditions::cathode','bc mode','steady state');
setnamed('CHARGE::boundary conditions::cathode','sweep type','single');
setnamed('CHARGE::boundary conditions::cathode','voltage',0);

setnamed('simulation region','x',x_center); 
setnamed('simulation region','x span',x_span-0.1e-6);
setnamed('CHARGE','solver type','newton');
setnamed('simulation region','x min',x_center);
 
save('pn_wg_R.ldev');
run;

test_result = getresult('CHARGE','r_test');
Itest = pinch(test_result.I);
Itest_max = abs(Itest(length(Itest)));
?"R_cathode = " + num2str(vtest_max/Itest_max * getnamed('CHARGE','norm length')/0.01) + " Ohm-cm";

switchtolayout;
setnamed('simulation region','x',x_center); 
setnamed('simulation region','x span',x_span-0.1e-6);
setnamed('simulation region','x max',x_center);
 
run;

test_result = getresult('CHARGE','r_test');
Itest = pinch(test_result.I);
Itest_max = abs(Itest(length(Itest)));
?"R_anode = " + num2str(vtest_max/Itest_max * getnamed('CHARGE','norm length')/0.01) + " Ohm-cm";


